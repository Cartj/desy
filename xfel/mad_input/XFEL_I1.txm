!option, -echo

!------------------------------------------------------------------------------
! Last work: 16 December 2011
!------------------------------------------------------------------------------
D00645 : DRIFT, L = 0.0645
D0082 : DRIFT, L = 0.082
D00052 : DRIFT, L = 0.0052
D03236: DRIFT, L = 0.3236
D0276 : DRIFT, L = 0.276
D0208 : DRIFT, L = 0.208
D04488: DRIFT, L = 0.4488
D12744 : DRIFT, L = 1.2744
D0285: DRIFT, L=0.285
! LOLA CAVITY FOR DIAGNOSTICS (MAD definition---------------------------------

I1.TDSA: LCAVITY, L=0.7, PHI0=0,               &
                VOLT=0,    &
                FREQ=2800000000.0*1e-6

! SECTION MARKERS --------------------------------------------------------------
I1.START.I1:   MARKER
I1.END.I1:     MARKER
I1.GUN:        MARKER
I1.S2E:        MARKER
I1.START.DARM: MARKER
I1.END.DARM:   MARKER
I1.START.LH:   MARKER
I1.END.LH:     MARKER
I1.START.DIA:  MARKER
I1.START.DOG:  MARKER
I1.END.DOG:    MARKER
I1.START.MID:  MARKER
I1.END.MID:    MARKER
I1.START.I1D:  MARKER
I1D.START.I1D: MARKER
I1D.END.I1D:   MARKER

! Diagnostics Marker -----------------------------------------------------------
I1.BPMA  :    MONITOR  
I1.BPMB  :    MONITOR   
I1.BPMG  :    MONITOR 
I1.BPMF:      MONITOR
I1D.BPMA :    MONITOR 
I1D.BPMD :    MONITOR 
I1.BPMS  :    MONITOR  
I1.SCRN:       MARKER
I1.OTRA:      MARKER
I1.OTRL:      MARKER
I1.OTRS:      MARKER
I1.OTRC:      MARKER
I1D.OTRC :     MARKER
I1D.OTRD :     MARKER
I1.DCM:       MARKER
I1.BAM:       MARKER
I1.TORA:      MARKER
I1.TORB:      MARKER
I1D.TORC :     MARKER
I1.EOD:       MARKER
I1.SRM:       MARKER
I1.FCUP:       MARKER
I1D.BHM:     MARKER

! Undulator with natural focusing--------------------------------------------
! 15.06.2012: Model using analytical matrix.
!    Period = 74 mm
!    Gap    = 32 - ?? mm  => Field  = 0.28 - 0.1 T
EnerLH  = 0.13;
gammaLH = EnerLH / EMASS;
bbetaLH = 1. / sqrt(1 - 1./(gammaLH^2));
LundLH    = 0.074;
NundLH    = 0.74 / LundLH;    ! = 10
LundLH_10= LundLH * 10.;
BundLH    = 0.28;         
KundLH    = 0.934*LundLH*100*BundLH;
kQundLH   = (2 * pi * KundLH) / (LundLH * gammaLH * bbetaLH);
OmegaLH   = kQundLH / sqrt(2.);
   
I1.UNDU: MATRIX, L = LundLH_10,          &
   RM11 = 1.0,                          &
   RM12 = LundLH_10,                    &
   RM13 = 0.0,                          &
   RM22 = 1.0,                          &
   RM33 = cos(OmegaLH*LundLH_10),        &
   RM34 = sin(OmegaLH*LundLH_10)/OmegaLH, &
   RM43 =-sin(OmegaLH*LundLH_10)*OmegaLH, &
   RM44 = cos(OmegaLH*LundLH_10)

l_lhbp  = LundLH/2.;
ang_lhbp = 0.2998*BundLH/sqrt(2)/EnerLH*l_lhbp;
arc_lhbp = (0.5 * l_lhbp * ang_lhbp) / sin(0.5 * ang_lhbp);

LH.BP.1: Sbend, L = arc_lhbp/2., ANGLE = ang_lhbp/2.,   &
            e1 = 0, e2 = ang_lhbp/2.
LH.BP.2: Sbend, L = arc_lhbp, ANGLE =-ang_lhbp,   &
            e1 = -ang_lhbp, e2 = -ang_lhbp
LH.BP.3: Sbend, L = arc_lhbp/2., ANGLE = ang_lhbp/2.,   &
            e1 = ang_lhbp/2., e2 = 0

!I1.UNDULATOR: Line=(D0030,10*(LH.BP.1,LH.BP.2,LH.BP.3),D0030)
I1.UNDULATOR: Line = (D0030,I1.UNDU,D0030)
!I1.UNDULATOR: DRIFT, L=0.800


I1.BK: SBEND, L =0.3, ANGLE=45/180*PI  ! To diagnostik arm
I1.SOLA: SOLENOID, L=0
I1.SOLB: SOLENOID, L=0

! Vacuum System Marker
I1.VCDST: DRIFT, L=1.215   ! Differential pump station
I1.VCBSHUT: DRIFT, L =0.3  ! Beam Stop

I1D.DUMP: MARKER
I1D.DUSTART: MARKER


I1.BSEC: RBEND,L=0.3,ANGLE=0
I1.CIX:   HKICKER, L=0.100
I1.CIY:   VKICKER, L=0.100

I1.CKXY:   KICKER, L=0.050

I1.KAX: HKICKER, L=0.350
I1.KDC:  VKICKER, L=0.200

!Definition of Collimator ---------------------------------
I1.COLO:   ECOL, XSIZE=0.02, YSIZE = 0.02
I1.COLU:   ECOL, XSIZE=0.02, YSIZE = 0.02
I1.DCCOL:   ECOL, XSIZE=0.02, YSIZE = 0.02

BPM : LINE= (D0050, I1.BPMB, D0050)

!----------------- laser heater Quadrupole ------------------------
I1.QI.1: Quadrupole, L=0.2, K1=-9.321360E-02  
I1.QI.2: Quadrupole, L=0.2, K1= 8.875443E-01 

!----------------- matching to diagnostics ------------------------
I1.QI.3: Quadrupole, L=0.2, K1= -7.654263E-01
I1.QI.4: Quadrupole, L=0.2, K1= -7.117866E-02    

!----------------- TDS Quadrupole ---------------------------------
I1.QI.5: Quadrupole, L=0.2, K1=2.494601E+00  
I1.QI.6: Quadrupole, L=0.2, K1=9.480791E-01 

!----------------- Diagnostics Quadrupole -------------------------
I1.QI.7: Quadrupole, L=0.2, K1=-4.15374
I1.QI.8: Quadrupole, L=0.2, K1=4.15355   
I1.QI.9: Quadrupole, L=0.2, K1=I1.QI.7[K1]

!----------------- matching to shielding and dogleg ---------------
I1.QI.11: Quadrupole, L=0.2, K1=2.128  !norm vers
!I1.QI.11:  Quadrupole, L = 0.20, K1 = -5.18005 !dump vers
I1.QI.12: Quadrupole, L=0.2, K1=0.933  !norm vers
!I1.QI.12: Quadrupole, L = 0.20, K1 = 3.1208 !dump vers 7.92 for 3.00m, 
I1.QI.13: Quadrupole, L=0.2, K1= -2.2531364E+00  
I1.QI.14: Quadrupole, L=0.2, K1= 2.441008E+00  
I1.QI.15: Quadrupole, L=0.2, K1= -2.559564E+00   
I1.QI.16: Quadrupole, L=0.2, K1=  3.653292E+00 
I1.QI.17: Quadrupole, L=0.2, K1=  -4.341087E+00 

!----------------- BC0 Quadrupole ---------------------------------
I1.QI.26: Quadrupole, L=0.2, K1=  3.854329117467 
I1.QI.27: Quadrupole, L=0.2, K1= -3.56939610008
I1.QI.28: Quadrupole, L=0.2, K1=  0.780579004468 
I1.QI.29: Quadrupole, L=0.2, K1= -1.631481453547 
I1.QI.30: Quadrupole, L=0.2, K1=  1.761852654868 
!----------------- PS Quadrupole ----------------------------------
I1.QI.31: Quadrupole, L=0.2, K1=-1.8 
I1.QI.32: Quadrupole, L=0.2, K1=+1.8 

!----------------- L1U Quadrupole ---------------------------------
I1.QI.33: Quadrupole, L=0.2, K1=  1.184851276534 
I1.QI.34: Quadrupole, L=0.2, K1=  0.638691311326
I1.QI.35: Quadrupole, L=0.2, K1= -1.118664445314 

!----------------- DUMP Quadrupole --------------------------------
I1D.QI.41:  Quadrupole, L = 0.20, K1 = -5.984535E+00
I1D.QI.42:  Quadrupole, L = 0.20, K1 =  5.141144E+00 ! 7.22 for 3.00m,


!----------------- Dogleg Magnets: Dogleg with R56=0.03m ----------
I1.QIH.18: Quadrupole, L=0.10, K1 = +5.444766974
I1.QIH.22: Quadrupole, L=0.10, K1 = I1.QIH.18[K1]
I1.QI.18:  Quadrupole, L=0.20, K1 = +5.444766974
I1.QI.22:  Quadrupole, L=0.20, K1 = I1.QI.18[K1]
I1.QI.19:  Quadrupole, L=0.20, K1 = -5.865493569
I1.QI.21:  Quadrupole, L=0.20, K1 = I1.QI.19[K1]
I1.QI.23:  Quadrupole, L=0.20, K1 = I1.QI.19[K1]
I1.QI.20:  Quadrupole, L=0.20, K1 = +5.906955582
I1.QI.24:  Quadrupole, L=0.20, K1 = I1.QI.20[K1]

I1.QI.25: Quadrupole, L=0.20, K1 =  -0.525034487046 

I1.SC.1.1:  SEXTUPOLE, L = 0.10, K2 = -98.1752276194943, tilt=pi/2
I1.SC.1.2:  SEXTUPOLE, L = 0.10, K2 = +98.1752276194943, tilt=pi/2
I1.SC.2.1:  SEXTUPOLE, L = 0.10, K2 = -59.4821133431329, tilt=pi/2
I1.SC.2.2:  SEXTUPOLE, L = 0.10, K2 = +59.4821133431329, tilt=pi/2

lll_injdip = 0.2

cur_injdip1 = -0.5545855173;
ang_injdip1 = 2.0 * asin(0.5 * lll_injdip * cur_injdip1);
arc_injdip1 = ang_injdip1 / cur_injdip1;
end_injdip1 = 0.5 * ang_injdip1;

cur_injdip2 = 0.2132461253;
ang_injdip2 = 2.0 * asin(0.5 * lll_injdip * cur_injdip2);
arc_injdip2 = ang_injdip2 / cur_injdip2;
end_injdip2 = 0.5 * ang_injdip2;

I1.BL.6.1: Sbend, L = arc_injdip1, ANGLE = ang_injdip1,   &
            e1 = end_injdip1, e2 = end_injdip1, tilt=pi/2
I1.BL.6.2: Sbend, L = arc_injdip1, ANGLE =-ang_injdip1,   &
            e1 = -end_injdip1, e2 = -end_injdip1, tilt=pi/2

I1.BL.8.1: Sbend, L = arc_injdip1, ANGLE = ang_injdip1,   &
            e1 = end_injdip1, e2 = end_injdip1, tilt=pi/2
I1.BL.8.2: Sbend, L = arc_injdip1, ANGLE =-ang_injdip1,   &
            e1 = -end_injdip1, e2 = -end_injdip1, tilt=pi/2

I1.BL.7.1: Sbend, L = arc_injdip2, ANGLE = ang_injdip2,   &
            e1 = end_injdip2, e2 = end_injdip2, tilt=pi/2
I1.BL.7.2: Sbend, L = arc_injdip2, ANGLE =-ang_injdip2,   &
            e1 = -end_injdip2, e2 = -end_injdip2,tilt=pi/2


!----------------- Dump Dipole ------------------------------------
I1.BB.0: DRIFT, L    = 0.5    ! Placeholder in straight beamline for I1D.BB.5
l_i1d  = 0.5
ang_i1d= 30./180.*acos(-1)
arc_i1d= l_i1d/sin(0.5*ang_i1d)*0.5*ang_i1d !l_b0/sin(ang_b0)*ang_b0


!ANG_I1D = 30/180*acos(-1);
!LEN_BB= 0.5;
!ARCLEN_I1D = (0.5 * LEN_BB * ANG_I1D) / sin(0.5 * ANG_I1D);
I1D.BB.5 : SBEND, L=arc_i1d, ANGLE=ang_i1d, E1=ang_i1d/2., E2=ang_i1d/2.

!----------------- Laser Heater Magnets ---------------------------
l_bl  = 0.2;
ang_lh = -0.099484
arc_lh = (l_bl * ang_lh) / sin(ang_lh);

I1.BL.1.1: sbend, l = arc_lh, angle = ang_lh,   &
                e1 = 0,   e2 = ang_lh
I1.BL.1.2: sbend, l = arc_lh, angle = -ang_lh,   &
                e1 = -ang_lh,   e2 = 0
I1.BL.3: sbend, l = arc_lh, angle = -ang_lh,   &
                e1 = 0,   e2 = -ang_lh
I1.BL.4: sbend, l = arc_lh, angle = ang_lh,   &
                e1 = ang_lh,   e2 = 0

!----------------- Injector from gun to ACC1 ----------------------

I1U      : LINE= (I1.GUN, I1.SOLA, D0276, I1.SOLB, D03236, D0100, &
                   D0050, I1.CKXY, I1.KDC, D0100, &
                   I1.BPMG, I1.SCRN, I1.FCUP, D0015, I1.START.DARM,&
                   D0300, D0300, I1.CKXY, &
                   I1.BPMG, I1.SCRN, D0100, &
                   D0020,  D0020, I1.DCCOL, D0200, I1.DCM, D0100, &
                   I1.TORB, D0045, I1.CKXY, D04488)

!----------------- Injector Dispersive Arm   ----------------------

I1DARM      : LINE= (I1.GUN, I1.SOLA, D05996, I1.SOLB, D0100, &
                   D0050, I1.CKXY, I1.KDC, D0100, &
                   I1.BPMG, I1.SCRN, I1.FCUP, D0015, I1.START.DARM,&
                   I1.BK, D0200, I1.BPMG, I1.SCRN, I1.FCUP, I1.END.DARM)

!----------------- Laser Heater -----------------------------------


DELTALH  = 0.100
CD0100I: DRIFT, L = DELTALH / cos(ang_lh)
D0043 : DRIFT, L=0.043
D00002: DRIFT, L=0.0002
D0064 : DRIFT, L=0.064
D01087: DRIFT, L=0.1087
D019625: DRIFT, L=0.19625
D027175: DRIFT, L=0.27175
D00785: DRIFT, L=0.0785
D010849: DRIFT, L=0.10849

D0003: DRIFT, L=0.003
D01975: DRIFT, L=0.1975
D02025: DRIFT, L=0.2025
D0122: DRIFT, L=0.122
D0260: DRIFT, L=0.260
D0035: DRIFT, L=0.035
D01817: DRIFT, L=0.1817
D00183: DRIFT, L=0.0183
D02739: DRIFT, L=0.2739
D00835: DRIFT, L=0.0835
! my drift
D03975: DRIFT, L=0.3975
D0085: DRIFT, L=0.085
D03175: DRIFT, L=0.3175
D0325: DRIFT ,L = 0.325

I1LH1     : LINE= &
                (D12744,I1.TORA,D02739,D0100,I1.QI.1,D0130, &
                 I1.BAM, D0170, I1.BPMF, D0150, I1.DCM, D0150, &
                 D00785, I1.QI.2, D0100)

I1LH2     : LINE = (I1.START.LH, &
                 D0043, (I1.BL.1.1), CD0100I, &
                 (I1.BL.1.2), D019625, I1.BPMF, &
                 D027175, I1.OTRL, D0064, D0100, &
                 I1.UNDULATOR, D0100, D0064,&
                 I1.OTRL,D0128,&
                 (I1.BL.3), CD0100I, (I1.BL.4), D0043, &
                 I1.END.LH)

I1LH : LINE=(I1LH1, I1LH2)


!------------------ Diagniostcis ----------------------------------
I1DIAU   : LINE= (D0100, I1.QI.3, D010849, D0100, I1.EOD,  &
                 D0200, I1.CIY, D0100, &
                 D0200, D0003, I1.CIX, D01975, I1.BPMF, D02025, &
                 I1.QI.4, D0070)
                 
diafodo: line=( D0035,I1.OTRC,D0125,& 
                   D0050,I1.CIY,D0150,I1.BPMA,D0150,&
                   I1.QI.7,D0105,I1.KAX,D0285,D0035,I1.OTRC,D0125,&
                   D0050,I1.CIX,D0150,I1.BPMA,D0150,&
                   I1.QI.8,D0105,I1.KAX,D0285)

I1DIACORE: LINE = (I1.TDSA,D0070,&
                   I1.QI.5,D0122,D0175,I1.KAX,D0105,&
                   I1.QI.6,D0105,I1.KAX,D0285,I1.START.DIA,D0035,I1.OTRC,D0125,& 
                   D0050,I1.CIY,D0150,I1.BPMA,D0150,&
                   I1.QI.7,D0105,I1.KAX,D0285,D0035,I1.OTRC,D0125,&
                   D0050,I1.CIX,D0150,I1.BPMA,D0150,&
                   I1.QI.8,D0105,I1.KAX,D0285,D0035,I1.OTRC,D0125, &
                   D0150,I1.CIY,D0050,D0150,& 
                   I1.QI.9,D0220,I1.BPMA,D0380,I1.OTRC,D0200)

I1DIAD1 : LINE =  (D0100,I1.QI.11,D0100,D01817,I1.TORA,D00183,D0350,I1.BPMA,&
                   D0100, D0300, I1.QI.12, D0150,  &
                   D0400, I1.START.I1D)

I1DIAD2: LINE =      (I1.BB.0, &                  
                      D0200, D0200, D0040,D0100, &                   
                      D0200, I1.CIY, D0100, I1.QI.13, D0150, I1.BPMA, &
                      D0200, I1.BSEC, D0950, I1.CIX,&
                      D0150, I1.VCBSHUT,D0200,I1.QI.14)                   
                   
I1DIA : LINE = (I1DIAU, I1DIACORE, I1DIAD1, I1DIAD2)


!------------------ New Dogleg with R56 = 0.03 m (compression) ------------

I1DLGU    : LINE= (D2900, &
                   I1.QI.15, D1800, I1.QI.16, &
                   D0700, I1.BPMA, D0200, I1.QI.17, D0200, &
                   I1.CIY, D0200, I1.CIX, D0100)

D02012268: Drift, L = 0.2012268540
D00804818: Drift, L = 0.0804818046
D02804818: Drift, L = 0.2804818046
D01409819: Drift, L = 0.1409819890
D01086082: Drift, L = 0.1086082922
D05086082: Drift, L = 0.5086082922
D03586082: Drift, L = 0.3586082922

!fodo_injdog: Line = (I1.QIH.18, D02012268, I1.BL.6.1,     &
!                     D0200, I1.BPMA, D02804818,           &
!                     I1.SC.1.1, D0100, I1.QI.19, D0100,   &
!                     I1.SC.2.1, D01409819, I1.BL.7.1,     &
!                     D03586082, I1.BPMA, D0150, I1.QIH.20)

DLG1A: LINE = (I1.QIH.18, D02012268, I1.BL.6.1,           &
               D0150,I1.CIX,D0150,D00804818,              &
               I1.SC.1.1, D0100, I1.QI.19, D0100,         &
               I1.SC.2.1, D01409819, I1.BL.7.1,           &
               D01086082,D0050,I1.BPMA,D0100,I1.CIY,D0150,&
               I1.QI.20, D0150, I1.CIX, D0150, D01086082, &
               I1.BL.7.1, D01409819, I1.SC.2.1,           &
               D0100, I1.QI.19, D0100, I1.SC.1.1,         &
               D00804818,D0050,D0100,D0100,I1.BPMA,D0150,&
               I1.BL.6.1, D02012268, I1.QIH.18)
DLG1B: LINE = (I1.QIH.18,D02012268, I1.BL.6.1,            &
               D0150, I1.CIX, D0150,D00804818,            &
               I1.SC.1.1, D0100, I1.QI.19, D0100,         &
               I1.SC.2.1, D01409819, I1.BL.7.1,           &
               D01086082,D0050,I1.BPMA,D0100,I1.CIY,D0150,&
               I1.QI.20, D0150, I1.CIX, D0150, D01086082, &       
               I1.BL.7.1, D01409819, I1.SC.2.1,           &
               D0100, I1.QI.21, D0100, I1.SC.1.1,         &
               D00804818,D0050,D0100,D0100,I1.BPMA,D0150,&
               I1.BL.6.1, D02012268, I1.QIH.22)
DLG3A: LINE = (I1.QIH.22,D02012268, I1.BL.8.2,            &
               D0150,I1.CIX,D0150,D00804818,              &
               I1.SC.1.2, D0100, I1.QI.21, D0100,         &
               I1.SC.2.2, D01409819, I1.BL.7.2,           &
               D01086082,D0050,I1.BPMA,D0100,I1.CIY,D0150,&
               I1.QI.24, D0150, I1.CIX, D0150, D01086082, &  
               I1.BL.7.2, D01409819, I1.SC.2.2,           &
               D0100, I1.QI.23, D0100, I1.SC.1.2,         &
               D00804818,D0050,D0100,D0100,I1.BPMA,D0150,&
               I1.BL.8.2, D02012268, I1.QIH.22)
DLG3B: LINE = (I1.QIH.22,D02012268, I1.BL.8.2,            &
               D0150,I1.CIX,D0150,D00804818,              &
               I1.SC.1.2, D0100, I1.QI.23, D0100,         &
               I1.SC.2.2, D01409819, I1.BL.7.2,           &
               D01086082,D0050,I1.BPMA,D0100,I1.CIY,D0150,&
               I1.QI.24, D0150, I1.CIX, D0150, D01086082, & 
               I1.BL.7.2, D01409819, I1.SC.2.2,           &
               D0100, I1.QI.23, D0100, I1.SC.1.2,         &
               D00804818,D0050,I1.CIY,D0100,I1.BPMA,D0150,&
               I1.BL.8.2, D02012268, I1.QIH.22)
DLG3BM: LINE = (I1.QIH.22,D02012268, I1.BL.8.2,            &
               D0150,I1.CIX,D0150,D00804818,              &
               I1.SC.1.2, D0100, I1.QI.23, D0100,         &
               I1.SC.2.2, D01409819, I1.BL.7.2,           &
               D01086082,D0050,I1.BPMA,D0100,I1.CIY,D0150,&
               I1.QI.24, D0150, I1.CIX, D0150, D01086082, & 
               I1.BL.7.2, D01409819, I1.SC.2.2,           &
               D0100, I1.QI.23, D0100, I1.SC.1.2,         &
               D00804818,D0050,I1.CIY,D0100,I1.BPMA,D0150,&
               I1.BL.8.2, D02012268, I1.QI.25)


DLGCORE   : LINE= (I1.START.DOG, I1.QIH.18, DLG1A, DLG1B, DLG3A, &
                   DLG3BM, I1.END.DOG)

I1DLG : LINE = (I1DLGU, DLGCORE)


!------------------ Bunch Compressor 0 ----------------------------
!------------------ Define B1 Chicane------------------------------
l_b0  = 0.5
ang_b0 = 0.132729704703170
arc_b0=l_b0/sin(ang_b0)*ang_b0
DELTAL  = 1.0
CD1000I = DELTAL / cos(ang_b0)

I1.BB.1.1 : SBEND,L=arc_b0,ANGLE= ang_b0, &
   e1= 0,e2= ang_b0,TILT = 1.570796326795  
I1.BB.1.2 : SBEND,L=arc_b0,ANGLE=-ang_b0, &
   e1= -ang_b0,e2=0,TILT = 1.570796326795  
I1.BB.1.3 : SBEND,L=arc_b0,ANGLE=-ang_b0, &
   e1= 0,e2=-ang_b0,TILT = 1.570796326795  
I1.BB.1.4 : SBEND,L=arc_b0,ANGLE= ang_b0, &
   e1=ang_b0,e2=0,TILT = 1.570796326795
D1000I : DRIFT, L=CD1000I
D1000IM : DRIFT, L=CD1000I-0.6 

I1.CBB.2: VKICKER, L=0
I1.CBB.3: VKICKER, L=0
I1.CBB.4: VKICKER, L=0

BC0U : LINE = (D000013, D0900, I1.TORA, D0200,&
               I1.CIY,D0110,I1.QI.26,D0300,I1.BPMF, &
               D0300, &
               I1.CIX,D0110,I1.QI.27,D0200, &
               D0200)

BC0D : LINE =     (D0200,&
                   D0100,D0110,I1.QI.28,D0110,I1.CIX,D0250,I1.BPMF, &
                   D0240, &
                   I1.CIY,D0110,I1.QI.29,D0200,I1.BPMA, &
                   D0500,D0060, &
                   I1.CIX,D0110,I1.QI.30)



BC0CORE: LINE =(I1.START.MID, &
                D0100, I1.BB.1.1, D1000I, I1.BB.1.2,I1.CBB.2, &
                D03975, I1.COLO,D0085,I1.COLU,D03175, D0100, I1.BPMS, D0100,&
                D0200, I1.OTRS, D0200, D0100, &
                I1.BB.1.3, I1.CBB.3, D0600, I1.SRM, D1000IM, &
                I1.BB.1.4, I1.CBB.4, D0100, &
                    I1.END.MID)

                   
I1BC0: LINE = (BC0U, BC0CORE, BC0D)

!----------------- Phase Shifter ----------------------------------                                

I1PS_FODO: LINE=(D0200,I1.BPMA,&
                D1800, D0090,&
                I1.CIY,D0110,I1.QI.31,D0200,I1.BPMA,&
                D1800,D0090,&
                I1.CIX,D0110,I1.QI.32)

I1.START.PS: MARKER                
I1PS:  LINE = ( &
                I1.START.PS,D0200,I1.BPMA,&
                D1800, D0090,&
                I1.CIY,D0110,I1.QI.31,D0200,I1.BPMA,&
                D1800,D0090,&
                I1.CIX,D0110,I1.QI.32,D0200,I1.BPMA,&
                D1800,D0090,&
                I1.CIY,D0110,I1.QI.31,D0200,I1.BPMA,&
                D0500,I1.VCDST,D0325)
                
!----------------- Match to Linac ---------------------------------
                                
L1U:  LINE = (  I1.CIX,D0110,I1.QI.33,D0200,I1.BPMA,&
                D1200,D0065,&
                I1.CIY,D0110,I1.QI.34,D0200,I1.BPMA,&
                D0500,D0090,&
                I1.TORA,D0400,I1.OTRA,D0300,I1.DCM,D0200,&
                I1.CIX,D0110,I1.QI.35,D0200,I1.BPMA,&
                D0315)

!----------------- Injector Dump ----------------------------------       
D0156: DRIFT, L=0.156
D0019: DRIFT, L=0.019
D026806: DRIFT, L=0.26806
D029694: DRIFT, L=0.29694
    

I1D30  : LINE = (I1D.START.I1D, I1D.BB.5, D0250, D0135, &
                  D0500, D0100, I1D.QI.41, D0175, &
                  I1D.BPMA, D0250, D0156, I1D.OTRC, D0150, D0019, &
                  I1D.QI.42, D026806, &
                  I1D.OTRD, D0225, I1D.TORC, D0125,I1D.BPMD, &
                  D029694, I1D.DUSTART, I1D.DUMP, I1D.BHM, I1D.END.I1D)                                    
                                 
!----------------- Basic Injector Lines ---------------------------

I1TDUMP: LINE = (I1.START.I1, I1U, L0, I1LH, &
                 I1DIAU, I1DIACORE, I1DIAD1, I1D30)
I1     : LINE = (I1.START.I1, I1U, L0, I1LH, I1DIA, &
                 I1DLG,I1BC0,I1PS,L1U,I1.END.I1)
I1S2E  : LINE = (I1.S2E, L0C8, I1LH, I1DIA, &
                 I1DLG, I1BC0, I1PS, L1U)
I1_GUN2BC0 : LINE=(I1.START.I1,I1U,L0,I1LH,I1DIA,I1DLG)     
BACKTR_ACC1_GUN : LINE= (L0C8REV,I1U,I1.START.I1)           


MATCH_I1: SUBROUTINE !----------------------------------------------------------

set,b0_i1.s2er[alfx],-b0_i1.s2e[alfx]
set,b0_i1.s2er[alfy],-b0_i1.s2e[alfy]
set,b0_i1.s2er[betx],b0_i1.s2e[betx]
set,b0_i1.s2er[bety],b0_i1.s2e[bety]

use,BACKTR_ACC1_GUN
savebeta,label=b0_i1.gun,I1.START.I1
!print,full
beam,energy=0.150278
twiss,beta0=b0_i1.s2er,save
plot,table=twiss,haxis=s,vaxis=betx,bety

set,b0_i1.gun[alfx],-b0_i1.gun[alfx]
set,b0_i1.gun[alfy],-b0_i1.gun[alfy]



use,I1_GUN2BC0
beam,energy=0.005
match,beta0=b0_i1.gun
vary,i1.q.a1.1[k1],step=0.1
vary,i1.q.ah1.1[k1],step=0.1
vary,i1.qi.1[k1],step=0.1
vary,i1.qi.2[k1],step=0.1
constraint, i1.start.lh, beta0=b0_i1.lh
lmdif
endmatch

match,beta0=b0_i1.gun
vary,i1.qi.3[k1],step=0.1
vary,i1.qi.4[k1],step=0.1
vary,i1.qi.5[k1],step=0.1
vary,i1.qi.6[k1],step=0.1
constraint, i1.start.dia, line=diafodo
lmdif
endmatch
twiss,beta0=b0_i1.gun,save


match,beta0=b0_i1.gun
vary,i1.q.a1.1[k1],step=0.1
vary,i1.q.ah1.1[k1],step=0.1
vary,i1.qi.1[k1],step=0.1
vary,i1.qi.2[k1],step=0.1
constraint, i1.start.dia, line=diafodo
lmdif
endmatch
print,full
twiss,beta0=b0_i1.gun,save

match,beta0=b0_i1.gun
vary,i1.qi.13[k1],step=0.1
vary,i1.qi.14[k1],step=0.1
vary,i1.qi.15[k1],step=0.1
vary,i1.qi.16[k1],step=0.1
vary,i1.qi.17[k1],step=0.1
constraint, i1.start.dog, beta0=b0_i1.dog
lmdif
endmatch
twiss,beta0=b0_i1.gun,save
plot,table=twiss,haxis=s,vaxis=betx,bety


use,I1PS_fodo
savebeta,label=i1ps_fodo.start,#s
twiss,save
plot,table=twiss,haxis=s,vaxis=betx,bety

title, "XFEL Injector "
use,I1
match,beta0=b0_i1.gun
vary,i1.qi.25[k1],step=0.1
vary,i1.qi.26[k1],step=0.1
vary,i1.qi.27[k1],step=0.1
vary,i1.qi.28[k1],step=0.1
vary,i1.qi.29[k1],step=0.1
vary,i1.qi.30[k1],step=0.1
constraint,I1.START.PS,beta0=I1PS_fodo.start
lmdif
simplex
endmatch

twiss,beta0=b0_i1.gun,save
plot,table=twiss,haxis=s,vaxis=betx,bety

title, "XFEL Injector+Linac1+BC1 "
set,L1.Q.A2.1[k1], 0.1  
set,L1.Q.A2.2[k1], -0.1  
set,L1.Q.A2.3[k1], 0.1  
set,L1.Q.A2.4[k1], -0.1
set,b1.qi.1[k1], -0.0
set,b1.qi.2[k1], 0.0
set,b1.qd.3[k1], 0.5
set,b1.qd.4[k1], -0.5

use,(i1,l1,b1)
match,beta0=b0_i1.gun
vary,i1.qi.33[k1],step=0.1
vary,i1.qi.34[k1],step=0.1
vary,i1.qi.35[k1],step=0.1
vary,L1.Q.A2.1[k1],step=0.1  
vary,L1.Q.A2.2[k1],step=0.1  
vary,L1.Q.A2.3[k1],step=0.1  
vary,L1.Q.A2.4[k1],step=0.1
!vary,b1.qi.1[k1],step=0.1
!vary,b1.qi.2[k1],step=0.1
vary,b1.qd.3[k1],step=0.1
vary,b1.qd.4[k1],step=0.1
constraint,I1.QI.25/b1.end.mid,betx<80,bety<80
constraint,b1.end.mid,beta0=b0_b1.mend  
!constraint, #e, beta0=b0_l1.start
lmdif
simplex
endmatch

savebeta,label=b0_l1.start,l1.start.l1
savebeta,label=b0_b1.start,b1.start
savebeta,label=b0_i1.lh,i1.start.lh

twiss,beta0=b0_i1.gun,save
plot,table=twiss,haxis=s,vaxis=betx,bety

value,i1.q.a1.1[k1]
value,i1.q.ah1.1[k1]
value,i1.qi.1[k1]
value,i1.qi.2[k1]
value,i1.qi.3[k1]
value,i1.qi.4[k1]
value,i1.qi.5[k1]
value,i1.qi.6[k1]
value,i1.qi.7[k1]
value,i1.qi.8[k1]
value,i1.qi.9[k1]
value,i1.qi.11[k1]
value,i1.qi.12[k1]
value,i1.qi.13[k1]
value,i1.qi.14[k1]
value,i1.qi.15[k1]
value,i1.qi.16[k1]
value,i1.qi.17[k1]
value,i1.qi.18[k1]
value,i1.qi.19[k1]
value,i1.qi.20[k1]
value,i1.qi.21[k1]
value,i1.qi.22[k1]
value,i1.qi.23[k1]
value,i1.qi.24[k1]
value,i1.qi.25[k1]
value,i1.qi.26[k1]
value,i1.qi.27[k1]
value,i1.qi.28[k1]
value,i1.qi.29[k1]
value,i1.qi.30[k1]
value,i1.qi.31[k1]
value,i1.qi.32[k1]
value,i1.qi.33[k1]
value,i1.qi.34[k1]
value,i1.qi.35[k1]
value,i1d.qi.41[k1]
value,i1d.qi.42[k1]
value,L1.Q.A2.1[k1]  
value,L1.Q.A2.2[k1]  
value,L1.Q.A2.3[k1]  
value,L1.Q.A2.4[k1]
value,b1.qi.1[k1]
value,b1.qi.2[k1]
value,b1.qd.3[k1]
value,b1.qd.4[k1]
ENDSUBROUTINE !-----------------------------------------------------------------

MATCH_I1PS: SUBROUTINE !----------------------------------------------------------


set,I1.QI.31[k1], I1.QI.31[k1] -0.5  
set,I1.QI.32[k1], I1.QI.32[k1] +0.5   
use,I1PS_fodo
savebeta,label=i1ps_fodo.start,#s
twiss,save

use,I1
match,beta0=b0_i1.gun
vary,i1.qi.25[k1],step=0.1
vary,i1.qi.26[k1],step=0.1
vary,i1.qi.27[k1],step=0.1
vary,i1.qi.28[k1],step=0.1
vary,i1.qi.29[k1],step=0.1
vary,i1.qi.30[k1],step=0.1
constraint,I1.START.PS,beta0=I1PS_fodo.start
lmdif
simplex
endmatch

use,(i1,l1,b1)
match,beta0=b0_i1.gun
vary,i1.qi.33[k1],step=0.1
vary,i1.qi.34[k1],step=0.1
vary,i1.qi.35[k1],step=0.1
vary,L1.Q.A2.1[k1],step=0.1  
vary,L1.Q.A2.2[k1],step=0.1  
vary,L1.Q.A2.3[k1],step=0.1  
vary,L1.Q.A2.4[k1],step=0.1
!vary,b1.qi.1[k1],step=0.1
!vary,b1.qi.2[k1],step=0.1
vary,b1.qd.3[k1],step=0.1
vary,b1.qd.4[k1],step=0.1
constraint,I1.QI.19/b1.end.mid,betx<80,bety<80
constraint,b1.end.mid,beta0=b0_b1.mend  
lmdif
simplex
endmatch

savebeta,label=b0_l1.start,l1.start.l1
savebeta,label=b0_b1.start,b1.start
savebeta,label=b0_I1.LH,i1.start.lh
twiss,beta0=b0_i1.gun,save
title, "XFEL Injector+Linac1+BC1 "
plot,table=twiss,haxis=s,vaxis=betx,bety
value, (b0_l1.start[mux]-b0_I1.LH[mux])


ENDSUBROUTINE !-----------------------------------------------------------------


MATCH_I1D: SUBROUTINE !----------------------------------------------------------
use,(I1TDUMP)
beam,energy=0.005
match,beta0=b0_i1.gun
vary,i1.qi.11[k1],step=0.1,lower=-7,upper=7
vary,i1.qi.12[k1],step=0.1,lower=-7,upper=7
vary,i1D.qi.41[k1],step=0.1,lower=-7,upper=7
vary,i1D.qi.42[k1],step=0.1,lower=-7,upper=7
constraint, #e, betx>1200,bety>1000
simplex
migrad
endmatch
twiss,beta0=b0_i1.gun,save
plot,table=twiss,haxis=s,vaxis=betx,bety
ENDSUBROUTINE !-----------------------------------------------------------------


LIST_I1: SUBROUTINE !-----------------------------------------------------------
use,(i1)
beam,energy=0.005
twiss,couple,beta0=b0_i1.gun, &
tape="mag_xfeli1.dat",save
plot,table=twiss,haxis=s,vaxis1=betx,bety,vaxis2=dx,dy, &
     spline,colour=100
plot,table=twiss,haxis=s,vaxis1=betx,bety,vaxis2=dx,dy, &
     spline,colour=100,hmin=0,hmax =50


survey,x0=0,z0=1,y0=0, &
phi0=phicor, tape="lay_xfeli1.dat"
makesequence,label=I1.seq,refer=centre

use,(i1lh2)
match
rmatrix,range=#s/#e, rm(1,1) = 0.
simplex,calls=1
endmatch

ENDSUBROUTINE !-----------------------------------------------------------------

